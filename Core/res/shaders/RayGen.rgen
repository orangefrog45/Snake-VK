#version 460
#extension GL_EXT_ray_tracing : enable
#extension GL_EXT_nonuniform_qualifier : enable
#extension GL_EXT_scalar_block_layout : enable

struct RayPayload {
  vec3 colour;
  vec3 normal;
  float distance;
  float roughness;
  uint mat_flags;
};

layout(location = 0) rayPayloadEXT RayPayload payload;

struct ShadowRayPayload {
  bool hit;
};

layout(location = 1) rayPayloadEXT ShadowRayPayload shadow_payload;

layout(set = 1, binding = 0) uniform accelerationStructureEXT as;
layout(set = 1, binding = 2, scalar) readonly buffer VertexPositionBuf { vec3 p[]; } vert_positions;
layout(set = 1, binding = 3, scalar) readonly buffer IndexBuf { ivec3 i[]; } indices;
layout(set = 1, binding = 4, scalar) readonly buffer VertexNormalBuf { vec3 n[]; } vert_normals;
layout(set = 1, binding = 5, scalar) readonly buffer VertexTexCoordBuf { vec2 t[]; } vert_tex_coords;
layout(set = 1, binding = 6, scalar) readonly buffer VertexTangentBuf { vec3 t[]; } vert_tangents;

layout(set = 1, binding = 1, rgba8) uniform writeonly image2D img_output;

layout(set = 1, binding = 9) uniform sampler2D sampler_gbuffer_albedo;
layout(set = 1, binding = 10) uniform sampler2D sampler_gbuffer_normals;
layout(set = 1, binding = 11) uniform sampler2D sampler_gbuffer_depth;
layout(set = 1, binding = 12) uniform sampler2D sampler_gbuffer_rma;
layout(set = 1, binding = 13) uniform usampler2D sampler_gbuffer_mat_flags;
//layout(set = 1, binding = 16, rgba16) uniform image2D img_reservoirs;


#define LIGHT_BUFFER_DESCRIPTOR_SET_BINDING_OVERRIDE
#define LIGHT_BUFFER_DESCRIPTOR_SET_IDX 1
#define LIGHT_BUFFER_DESCRIPTOR_BINDING 8
#include "LightBuffers.glsl"

#define TRANSFORM_BUFFER_DESCRIPTOR_SET_IDX 1
#define TRANSFORM_BUFFER_DESCRIPTOR_BINDING 14
#include "Transforms.glsl"

#define RAYTRACING_INSTANCE_BUFFER_DESCRIPTOR_SET_IDX 1
#define RAYTRACING_INSTANCE_BUFFER_DESCRIPTOR_BINDING 7
#define RAYTRACING_EMISSIVE_BUFFER_DESCRIPTOR_BINDING 16
#include "RaytracingInstances.glsl"

struct TriangleData {
  // The object the triangle belongs to
  uint object_instance_idx;

  // The index of the triangle's indices in the index buffer
  uint triangle_idx;

  // The barycentric coordinates for the point inside the triangle
  vec2 bary_coords;
};

struct PixelReservoir {
  // Pointlights
  float w_sum_pl;
  float w_pl;
  uint pl_idx;
  uint pl_m;

  // Emissives
  TriangleData emissive_triangle;
  float w_sum_em;
  float w_em;
  uint m_em;
  float p_hat_em;
  vec3 radiance_em;

  // Down here for alignment
  float p_hat_pl;
};

layout(set = 1, binding = 17) buffer ReservoirBuf { PixelReservoir r[]; } reservoirs;
layout(set = 1, binding = 18) uniform sampler2D sampler_pixel_motion;
layout(set = 1, binding = 19) uniform sampler2D sampler_prev_frame_depth;
layout(set = 1, binding = 20) uniform sampler2D sampler_prev_frame_normals;

layout(push_constant) uniform pc {
    uint initial_reservoir_pass;
    int num_temporal_resamples;
    int num_spatial_resamples;
} push;

#include "CommonUBO.glsl"
#include "Util.glsl"

#define TEX_MAT_DESCRIPTOR_SET_IDX 2
#include "TexMatBuffers.glsl"

#define LIGHT_RIS_ITERS 32u
#define INVALID_RESERVOIR_IDX ~0u



float ShadowTest(vec3 ro, vec3 rd, float max_dist) {
  // Set initially to true, miss shader sets to false
  shadow_payload.hit = true;

  uint flags = gl_RayFlagsTerminateOnFirstHitEXT | gl_RayFlagsOpaqueEXT | gl_RayFlagsSkipClosestHitShaderEXT;
  traceRayEXT(
    as,
    flags,
    0xff,
    0,
    0,
    1,
    ro,
    0.001,
    rd, 
    max_dist,
    1
  );

  return 1.0 - float(shadow_payload.hit);
}

bool UpdatePointlightReservoir(inout PixelReservoir reservoir, uint pl_idx, float weight, float rnd_seed) {
  reservoir.pl_m++;
  const float rnd = gold_noise(vec2(gl_LaunchIDEXT.xy), fract(weight+common_ubo.time_elapsed*0.0009783 + rnd_seed));

  reservoir.w_sum_pl += weight;
  if (rnd < weight / reservoir.w_sum_pl) {
    reservoir.pl_idx = pl_idx;
    return true;
  }

  return false;
}

bool UpdateEmissiveReservoir(inout PixelReservoir reservoir, TriangleData triangle, float weight, float rnd_seed) {
  reservoir.m_em++;
  const float rnd = gold_noise(vec2(gl_LaunchIDEXT.xy), fract(weight+common_ubo.time_elapsed*0.000789 + rnd_seed));

  reservoir.w_sum_em += weight;
  if (rnd < weight / reservoir.w_sum_em) {
    reservoir.emissive_triangle = triangle;
    return true;
  }

  return false;
}

vec3 ComputeTriangleRadiance(TriangleData triangle, vec3 v, vec3 f0, vec3 world_pos, vec3 n, float roughness, float metallic, vec3 albedo, vec3 biased_pos, bool shadow_test) {
  InstanceData instance = rt_instances.i[triangle.object_instance_idx];
  ivec3 ti = indices.i[triangle.triangle_idx + instance.mesh_buffer_index_offset / 3] + ivec3(instance.mesh_buffer_vertex_offset);

  vec3 bary = vec3(triangle.bary_coords, 1.0 - triangle.bary_coords.x - triangle.bary_coords.y);

  vec3 p = bary.x * vert_positions.p[ti.x] + bary.y * vert_positions.p[ti.y] + bary.z * vert_positions.p[ti.z];
  vec3 point_normal = bary.x * vert_normals.n[ti.x] + bary.y * vert_normals.n[ti.y] + bary.z * vert_normals.n[ti.z];

  p = vec4(transforms.m[instance.transform_idx] * vec4(p, 1.0)).xyz;
  point_normal = normalize(transpose(inverse(mat3(transforms.m[instance.transform_idx]))) * point_normal);

  vec3 emissive = material_ubo.materials[instance.material_idx].albedo.xyz;
  vec3 brdf = CookTorranceBRDF(p, world_pos, v, f0, n, roughness, metallic);

  vec3 radiance = emissive*albedo*brdf / abs(dot(world_pos - p, world_pos - p));

  if (shadow_test) {
    float point_dist = length(biased_pos - (p + point_normal * 0.01));
    vec3 dir = ((p + point_normal * 0.01) - biased_pos) / point_dist;
    radiance *= ShadowTest(biased_pos, dir, point_dist);
  }

  return radiance;
}

void InitPixelReservoir(inout PixelReservoir reservoir) {
  const ivec2 coord = ivec2(gl_LaunchIDEXT.xy);
  reservoir.pl_idx = INVALID_RESERVOIR_IDX;
  reservoir.w_sum_pl = 0.0;
  reservoir.pl_m = 0;
  reservoir.w_pl = 0.0;
  reservoir.p_hat_pl = 0.0;

  reservoir.emissive_triangle.object_instance_idx = INVALID_RESERVOIR_IDX;
  reservoir.emissive_triangle.triangle_idx = INVALID_RESERVOIR_IDX;
  reservoir.w_sum_em = 0.0;
  reservoir.m_em = 0;
  reservoir.w_em = 0.0;
  reservoir.p_hat_em = 0.0;
}

void DoInitialWRS(inout PixelReservoir reservoir, vec3 v, vec3 f0, vec3 world_pos, vec3 n, float roughness, float metallic, vec3 albedo, vec3 biased_pos) {
  for (uint i = 0u; i < LIGHT_RIS_ITERS; i++) {
    const uint rnd_idx = uint(gold_noise(vec2(gl_LaunchIDEXT.xy), fract(common_ubo.time_elapsed * 0.001197 + world_pos.x + world_pos.y + world_pos.z) + i / float(LIGHT_RIS_ITERS))
     * float(ssbo_light_data.num_pointlights));

    float p_hat = length(CalcPointlight(ssbo_light_data.pointlights[rnd_idx], v, f0, world_pos, n, roughness, metallic, albedo));
    float weight = p_hat / (1.0 / float(ssbo_light_data.num_pointlights));

    if (UpdatePointlightReservoir(reservoir, rnd_idx, weight, sin(i*20382997.34741))) {
      reservoir.p_hat_pl = p_hat;
    }
  }

  for (uint i = 0u; i < LIGHT_RIS_ITERS; i++) {
    uint rnd_emissive_idx = uint(gold_noise(vec2(gl_LaunchIDEXT.xy), abs(fract(world_pos.x+world_pos.y+world_pos.z+common_ubo.time_elapsed*0.001534 + i / float(LIGHT_RIS_ITERS))))
     * float(rt_emissive_instance_indices.num_emissive_instances));

    TriangleData triangle;

    // Pick random emissive object
    triangle.object_instance_idx = rt_emissive_instance_indices.i[rnd_emissive_idx];
    InstanceData instance = rt_instances.i[triangle.object_instance_idx];

    // Pick random triangle on object
    triangle.triangle_idx = uint(gold_noise(vec2(gl_LaunchIDEXT.xy), abs(fract(world_pos.x+world_pos.y+world_pos.z+common_ubo.time_elapsed*0.00119 + i / float(LIGHT_RIS_ITERS))))
     * float(instance.num_mesh_indices / 3)); 
     ivec3 ti = indices.i[triangle.triangle_idx + instance.mesh_buffer_index_offset / 3] + ivec3(instance.mesh_buffer_vertex_offset);

    // Pick random coordinates inside triangle
    triangle.bary_coords = vec2(random(sin(i*27934.3221+gl_LaunchIDEXT.x*293.28) + common_ubo.time_elapsed*0.001+i), random(sin(i*12391.8583+gl_LaunchIDEXT.y*983.3399) + ti.y + common_ubo.time_elapsed*0.0091+i));
    if (triangle.bary_coords.x + triangle.bary_coords.y >= 1.0) {
      triangle.bary_coords.x = 1.0 - triangle.bary_coords.x;
      triangle.bary_coords.y = 1.0 - triangle.bary_coords.y;
    }

    float triangle_area = 0.5 * length(cross(vert_positions.p[ti.y] - vert_positions.p[ti.x], vert_positions.p[ti.y] - vert_positions.p[ti.z]));

    vec3 radiance = ComputeTriangleRadiance(triangle, v, f0, world_pos, n, roughness, metallic, albedo, biased_pos, false);
    float p_hat = length(radiance);
    float pdf = 1.0 / (rt_emissive_instance_indices.num_emissive_instances * (instance.num_mesh_indices / 3) * triangle_area);
    float weight = p_hat / pdf;

    if (UpdateEmissiveReservoir(reservoir, triangle, weight, sin(i*38293.28347))) {
      reservoir.p_hat_em = p_hat;
      reservoir.radiance_em = radiance;
    }
  }

  reservoir.w_em = (1.0 / reservoir.p_hat_em) * (reservoir.w_sum_em / reservoir.m_em);
  reservoir.w_pl = (1.0 / reservoir.p_hat_pl) * (reservoir.w_sum_pl / reservoir.pl_m);
}



void CombinePixelReservoir(inout PixelReservoir current_pixel_reservoir, in PixelReservoir neighbour_reservoir, vec3 v, vec3 f0, vec3 world_pos, vec3 n, float roughness, float metallic, vec3 albedo, vec3 biased_pos, float rnd_seed) {
  // Pointlight reservoir
  if (neighbour_reservoir.pl_idx != INVALID_RESERVOIR_IDX) {
    float light_dist = length(ssbo_light_data.pointlights[neighbour_reservoir.pl_idx].pos.xyz - biased_pos);
    float p_hat = length(CalcPointlight(ssbo_light_data.pointlights[neighbour_reservoir.pl_idx], v, f0, world_pos, n, roughness, metallic, albedo)) *
     ShadowTest(biased_pos, (ssbo_light_data.pointlights[neighbour_reservoir.pl_idx].pos.xyz - biased_pos) / light_dist, light_dist - 0.001);

    float weight = p_hat * neighbour_reservoir.pl_m * neighbour_reservoir.w_pl;

    if (UpdatePointlightReservoir(current_pixel_reservoir, neighbour_reservoir.pl_idx, weight, rnd_seed)) {
      current_pixel_reservoir.p_hat_pl = p_hat;
    }
  }

  // Emissive triangle reservoir
  if (neighbour_reservoir.emissive_triangle.object_instance_idx != INVALID_RESERVOIR_IDX) {
    vec3 radiance = ComputeTriangleRadiance(neighbour_reservoir.emissive_triangle, v, f0, world_pos, n, roughness, metallic, albedo, biased_pos, true);
    float p_hat = length(radiance);

    float weight = p_hat * neighbour_reservoir.w_em * neighbour_reservoir.m_em;

    if (UpdateEmissiveReservoir(current_pixel_reservoir, neighbour_reservoir.emissive_triangle, weight, rnd_seed + 0.283673699)) {
      current_pixel_reservoir.p_hat_em = p_hat;
      current_pixel_reservoir.radiance_em = radiance;
    }
  }
}

void CreateCoordinateSystem(in vec3 n, out vec3 nt, out vec3 nb) {
  if (abs(n.x) > abs(n.y))
    nt = vec3(n.z, 0, -n.x) / length(n.xz);
  else
    nt = vec3(0, -n.z, n.y) / length(n.yz);

    nb = cross(n, nt);
}

vec3 UniformSampleHemisphere(float r1, float r2) {
  float sin_theta = sqrt(1 - r1 * r1);
  float phi = 2 * PI * r2;
  return vec3(sin_theta * cos(phi), r1, sin_theta * sin(phi));
}


vec3 BounceDiffuse(vec3 ro, vec3 normal, vec3 reflected_dir, vec3 albedo, float roughness) {
  return vec3(0);
  vec3 colour_sum = vec3(0);

  vec3 nt;
  vec3 nb;
  CreateCoordinateSystem(normal, nt, nb);

  vec3 nt_inner;
  vec3 nb_inner;
  vec3 n_inner;

  float l2_ro = dot(ro, ro);

  float seed = fract(l2_ro+ro.x+ro.y+ro.z+common_ubo.time_elapsed+common_ubo.delta_time);
  vec2 xy = gl_LaunchIDEXT.xy;
  xy += fract(vec2(sin(xy.x*1038.283726 + xy.y*common_ubo.delta_time), cos(xy.x*common_ubo.time_elapsed+xy.y*9278.33337)));
  for (uint ray_idx = 0; ray_idx < 8; ray_idx++) {
    seed = fract(seed + ray_idx/8.0);
    vec3 rnd = UniformSampleHemisphere(gold_noise(xy, fract(seed+ray_idx/8.0)), gold_noise(xy, fract(seed+ray_idx/8.0)+0.1));
    vec3 rd = vec3(
      rnd.x * nb.x + rnd.y * normal.x + rnd.z * nt.x,
      rnd.x * nb.y + rnd.y * normal.y + rnd.z * nt.y,
      rnd.x * nb.z + rnd.y * normal.z + rnd.z * nt.z
    );

    traceRayEXT(
      as,
      gl_RayFlagsOpaqueEXT,
      0xff,
      0,
      0,
      0,
      ro,
      0.01,
      rd, 
      1000,
      0
    );

    colour_sum += albedo * payload.colour * max(cos(dot(rd, normal)), 0.0) / 8.0;
    if (payload.distance < 0.f || bool(payload.mat_flags & MAT_FLAG_EMISSIVE)) {
      continue;
    }

    vec3 new_pos = ro + rd * payload.distance;
    n_inner = payload.normal;
    CreateCoordinateSystem(n_inner, nt_inner, nb_inner);

    for (uint bounce_idx = 0; bounce_idx < 2; bounce_idx++) {
      rd = UniformSampleHemisphere(gold_noise(xy, fract(seed+bounce_idx*0.5+0.2)), gold_noise(xy, fract(seed+bounce_idx*0.5+0.3)));
      rd = vec3(
      rd.x * nb_inner.x + rd.y * n_inner.x + rd.z * nt_inner.x,
      rd.x * nb_inner.y + rd.y * n_inner.y + rd.z * nt_inner.y,
      rd.x * nb_inner.z + rd.y * n_inner.z + rd.z * nt_inner.z
      );

      traceRayEXT(
        as,
        gl_RayFlagsOpaqueEXT,
        0xff,
        0,
        0,
        0,
        new_pos,
        0.01,
        rd, 
        1000,
        0
      );
      colour_sum += albedo * payload.colour * 0.25 * max(cos(dot(rd, n_inner)), 0.0) / 8.0;
    }
  }

  return colour_sum * (2 * PI);
}

// start_coord should be texture coordinates between 0 and 1
vec2 FindNeighbourPixelCoord(vec2 start_coord, float radius, vec2 seed) {
  ivec2 render_size = textureSize(sampler_pixel_motion, 0);

  vec2 offset = vec2(
    (gold_noise(vec2(gl_LaunchIDEXT.xy), fract(seed.x + common_ubo.time_elapsed * 0.0011)) - 0.5) * 2.0 * radius / float(render_size.x),
    (gold_noise(vec2(gl_LaunchIDEXT.xy), fract(seed.y + common_ubo.time_elapsed * 0.0006)) - 0.5) * 2.0 * radius / float(render_size.y)
  );

  // Keep searching for valid coordinate
  int i = 0;
  while (any(lessThan(start_coord + offset, vec2(0))) || any(greaterThan(start_coord + offset, vec2(1)))) {
    i++;
    offset = vec2(
      (gold_noise(vec2(gl_LaunchIDEXT.xy+i), fract(seed.x + sin(i*293.28937)+ common_ubo.time_elapsed * 0.0011)) - 0.5) * 2.0 * radius / float(render_size.x),
      (gold_noise(vec2(gl_LaunchIDEXT.xy+i), fract(seed.y + sin(i*293.28937)+ common_ubo.time_elapsed * 0.0006)) - 0.5) * 2.0 * radius / float(render_size.y)
    );
  }

  return start_coord + offset;
}

void WriteInitialReservoirs(vec3 v, vec3 f0, vec3 ro, vec3 normal, vec3 rma, vec3 albedo, vec3 biased_pos) {
  PixelReservoir pixel_reservoir;

  InitPixelReservoir(pixel_reservoir);
  DoInitialWRS(pixel_reservoir, v, f0, ro, normal, rma.r, rma.g, albedo, biased_pos);

  PixelReservoir temporal_reservoir;
  InitPixelReservoir(temporal_reservoir);
  
  // Temporal resample
  ivec2 render_size = textureSize(sampler_pixel_motion, 0);
  if (!common_ubo.prev_frame_invalid && push.num_temporal_resamples != 0) {
    // Reproject pixel
    vec2 pixel_velocity = texelFetch(sampler_pixel_motion, ivec2(gl_LaunchIDEXT.xy), 0).xy;
    vec2 history_tex_coord = (vec2(gl_LaunchIDEXT.xy) + 0.5) / vec2(render_size) + pixel_velocity;

    // Make sure pixel has a valid history
    if (all(greaterThan(history_tex_coord, vec2(0))) && all(lessThan(history_tex_coord, vec2(1)))) {
      float seed = biased_pos.x + biased_pos.y + biased_pos.z;
      //vec2 neighbour_coord = FindNeighbourPixelCoord(history_tex_coord, 30.0, vec2(seed, -seed));      
      vec2 neighbour_coord = history_tex_coord;      

      //float prev_depth = texture(sampler_prev_frame_depth, history_tex_coord + offset).r;
      vec3 neighbour_normal = texture(sampler_prev_frame_normals, neighbour_coord).rgb;
      if (dot(neighbour_normal, normal) > 0.95) {
        ivec2 neighbour_coord_pixel_space = ivec2(neighbour_coord * render_size);
        CombinePixelReservoir(temporal_reservoir, pixel_reservoir, v, f0, ro, normal, rma.r, rma.g, albedo, biased_pos, 0.846746997);
        CombinePixelReservoir(temporal_reservoir, reservoirs.r[neighbour_coord_pixel_space.y * render_size.x + neighbour_coord_pixel_space.x], v, f0, ro, normal, rma.r, rma.g, albedo, biased_pos, 0);
        
        const uint M_CAP = 30; 
        temporal_reservoir.m_em = min(reservoirs.r[neighbour_coord_pixel_space.y * render_size.x + neighbour_coord_pixel_space.x].m_em + pixel_reservoir.m_em, pixel_reservoir.m_em * M_CAP);
        temporal_reservoir.pl_m = min(reservoirs.r[neighbour_coord_pixel_space.y * render_size.x + neighbour_coord_pixel_space.x].pl_m + pixel_reservoir.pl_m, pixel_reservoir.pl_m * M_CAP);
        
        temporal_reservoir.w_em = temporal_reservoir.p_hat_em > 0.0 ? (1.0 / temporal_reservoir.p_hat_em) * (temporal_reservoir.w_sum_em / temporal_reservoir.m_em) : 0.0;
        temporal_reservoir.w_pl = temporal_reservoir.p_hat_pl > 0.0 ? (1.0 / temporal_reservoir.p_hat_pl) * (temporal_reservoir.w_sum_pl / float(temporal_reservoir.pl_m)) : 0.0;
      }
    }
  }

  // Write to a different part of the buffer to prevent race conditions
  const uint write_offset = render_size.x * render_size.y;
  reservoirs.r[write_offset + gl_LaunchIDEXT.y * render_size.x + gl_LaunchIDEXT.x] = temporal_reservoir;
}

PixelReservoir ComputeAndWriteSpatialReservoirReuse(vec3 v, vec3 f0, vec3 ro, vec3 normal, vec3 rma, vec3 albedo, vec3 biased_pos) {
  ivec2 render_size = textureSize(sampler_pixel_motion, 0);
  vec2 pixel_coord = vec2(gl_LaunchIDEXT.xy) / vec2(render_size);
  
  // Read from the section of the buffer that was previously written to during the temporal resample stage
  const uint read_offset = render_size.x * render_size.y;
  PixelReservoir pixel_reservoir = reservoirs.r[read_offset + gl_LaunchIDEXT.y * render_size.x + gl_LaunchIDEXT.x];
  float seed = biased_pos.x + biased_pos.y + biased_pos.z + common_ubo.delta_time;
  for (int i = 0; i < push.num_spatial_resamples; i++) {
    ivec2 neighbour_coord_pixel_space = ivec2(render_size * FindNeighbourPixelCoord(pixel_coord, 30.0, vec2(seed, -seed) + sin(i*13477.3793)));
    
    vec3 neighbour_normal = texelFetch(sampler_prev_frame_normals, neighbour_coord_pixel_space, 0).rgb;
    if (dot(neighbour_normal, normal) < 0.95) continue;

   // CombinePixelReservoir(pixel_reservoir, reservoirs.r[read_offset + neighbour_coord_pixel_space.y * render_size.x + neighbour_coord_pixel_space.x], v, f0, ro, normal, rma.r, rma.g, albedo, biased_pos);
  }

  // Write to section of buffer not currently being read from
  reservoirs.r[gl_LaunchIDEXT.y * render_size.x + gl_LaunchIDEXT.x] = pixel_reservoir;
  return pixel_reservoir;
}

vec3 CalculateRadianceFromReservoirs(PixelReservoir reservoir, vec3 v, vec3 f0, vec3 ro, vec3 normal, vec3 rma, vec3 albedo, vec3 biased_pos) {
  ivec2 render_size = textureSize(sampler_pixel_motion, 0);

  vec3 radiance = vec3(0);

  if (ssbo_light_data.num_pointlights > 0 && reservoir.pl_idx != INVALID_RESERVOIR_IDX) {
    float light_dist = length(ssbo_light_data.pointlights[reservoir.pl_idx].pos.xyz - biased_pos);
    vec3 pointlight_radiance = CalcPointlight(ssbo_light_data.pointlights[reservoir.pl_idx], v, f0, ro, normal, rma.r, rma.g, albedo.rgb) * 
      ShadowTest(biased_pos, (ssbo_light_data.pointlights[reservoir.pl_idx].pos.xyz - biased_pos) / light_dist, light_dist);

    pointlight_radiance *= reservoir.w_pl;
    radiance += pointlight_radiance;
  }

  if (rt_emissive_instance_indices.num_emissive_instances > 0 && reservoir.emissive_triangle.object_instance_idx != INVALID_RESERVOIR_IDX) {
    reservoir.radiance_em.xyz = ComputeTriangleRadiance(reservoir.emissive_triangle, v, f0, ro, normal, rma.r, rma.g, albedo, biased_pos, true);
    radiance += reservoir.radiance_em.xyz * reservoir.w_em;
  }

  return radiance;
}

void main() {
  vec2 pixel_center = vec2(gl_LaunchIDEXT.xy) + vec2(0.5);
  vec2 uv = pixel_center / vec2(gl_LaunchSizeEXT.xy);

  vec3 albedo = texelFetch(sampler_gbuffer_albedo, ivec2(gl_LaunchIDEXT), 0).rgb;

  const vec3 rma = texelFetch(sampler_gbuffer_rma, ivec2(gl_LaunchIDEXT), 0).rgb;
  const uint mat_flags = texelFetch(sampler_gbuffer_mat_flags, ivec2(gl_LaunchIDEXT), 0).r;


  if (bool(mat_flags & MAT_FLAG_EMISSIVE)) {
    imageStore(img_output, ivec2(gl_LaunchIDEXT), vec4(albedo, 1.0));
    return;
  } 

  vec3 normal = texelFetch(sampler_gbuffer_normals, ivec2(gl_LaunchIDEXT), 0).rgb;
  vec3 ro = WorldPosFromDepth(texelFetch(sampler_gbuffer_depth, ivec2(gl_LaunchIDEXT), 0).r, uv);

  vec3 v = normalize(common_ubo.cam_pos.xyz - ro);
	vec3 r = reflect(-v, normal);
	float n_dot_v = max(dot(normal, v), 0.0);

  vec3 f0 = vec3(0.04); // TODO: Support different values for more metallic objects
  f0 = mix(f0, albedo, rma.g);

  vec3 biased_pos = ro + normal * (0.01 + max(length(common_ubo.cam_pos.xyz - ro) * 0.001, 0.01));

  if (push.initial_reservoir_pass == 1) {
    WriteInitialReservoirs(v, f0, ro, normal, rma, albedo, biased_pos);
    return;
  }

  vec3 light = CalcDirectionalLight(v, f0, normal, rma.r, rma.g, albedo) * ShadowTest(biased_pos, ssbo_light_data.dir_light.dir.xyz, 1000);
  PixelReservoir reservoir = ComputeAndWriteSpatialReservoirReuse(v, f0, ro, normal, rma, albedo, biased_pos);
  light += CalculateRadianceFromReservoirs(reservoir, v, f0, ro, normal, rma, albedo, biased_pos);

  vec3 colour_sum = light;
  colour_sum += BounceDiffuse(biased_pos, normal, r, albedo, rma.r);

  imageStore(img_output, ivec2(gl_LaunchIDEXT), vec4(colour_sum, 1.0));
}